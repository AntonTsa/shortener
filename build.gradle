plugins {
    id 'checkstyle'
    id 'application'
    id 'jacoco'
    id "io.freefair.lombok" version "9.1.0"
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.anton.tsarenko'
version = '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation "org.flywaydb:flyway-database-postgresql"
    implementation 'io.github.cdimascio:dotenv-java:3.0.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'

    implementation 'org.postgresql:postgresql:42.7.9'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'

    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    compileOnly 'org.projectlombok:lombok:1.18.42'

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

checkstyle {
    toolVersion = '13.0.0'
    configFile = file("checkstyle.xml")
    ignoreFailures = false
}

checkstyleMain {
    source ='src/main/java'
}

checkstyleTest {
    source ='src/test/java'
}

application {
    // Define the main class for the application.
    mainClass = 'com.anton.tsarenko.shortener.AppLauncher'
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.14"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}